
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбщийВызовСервера.ЗаписатьНовоеЗначениеКонстанты("ПерезаписатьНапоминания",истина);
	Оповестить("Добавление элемента в раздел",Запись.Элемент);
КонецПроцедуры

&НаСервере
Процедура УдалитьНапоминаниеНаСервере(УдалятьЭлемент)
	Если УдалятьЭлемент Тогда
		ЭлемОбъект = запись.Элемент.ПолучитьОбъект();
		ЭлемОбъект.Удалить();
		Возврат;
	КонецЕсли;
	
	МенЗап = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
	МенЗап.Комментарий = запись.Комментарий;
	МенЗап.Элемент = запись.Элемент;
	МенЗап.Удалить();
	
	Если ЗначениеЗаполнено(запись.Периодичность) Тогда//Создается новое оповещение согласно периодичности
		МенЗап = РегистрыСведений.Напоминания.СоздатьМенеджерЗаписи();
		МенЗап.Комментарий = запись.Комментарий;
		МенЗап.Элемент = запись.Элемент;
		Если запись.Периодичность = "Час" Тогда
			МенЗап.ДатаНапоминания = ТекущаяДата() + 3600;//Просто увеличиваем на час
		ИначеЕсли запись.Периодичность = "День" Тогда
			МенЗап.ДатаНапоминания = запись.ДатаПериодическогоНапоминания + 24*3600 + (запись.ВремяНапоминания - дата(1,1,1)); //На завтра на указанное время
		ИначеЕсли запись.Периодичность = "Неделя" Тогда// На следующую неделю на указанное время
			МенЗап.ДатаНапоминания = запись.ДатаПериодическогоНапоминания + 7*24*3600 + (запись.ВремяНапоминания - дата(1,1,1));
		ИначеЕсли запись.Периодичность = "Месяц" Тогда// На следующий месяц на указанное время
			МенЗап.ДатаНапоминания = ДобавитьМесяц(запись.ДатаПериодическогоНапоминания,1) + (запись.ВремяНапоминания - дата(1,1,1));		
		КонецЕсли;
		МенЗап.ДатаПериодическогоНапоминания = МенЗап.ДатаНапоминания;
		МенЗап.ВремяНапоминания = запись.ВремяНапоминания;
		МенЗап.Периодичность = запись.Периодичность;
		МенЗап.Записать();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНапоминание(Команда)
	Если Периодическое Тогда
		УдалитьНапоминаниеПослеОтвета(ложь);
	Иначе	
		ПоказатьВопрос(новый ОписаниеОповещения("УдалитьНапоминаниеПослеОтвета",ЭтотОбъект),"Удалить так же элемент раздела?",РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНапоминаниеПослеОтвета(РезультатВопроса,ДополнительныеПараметры = ложь) экспорт //Ден  2016 10 31 ---
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;	
	Модифицированность = ложь;
	УдалитьНапоминаниеНаСервере(?(РезультатВопроса = КодВозвратаДиалога.Да,истина,ложь));
	ОбщийВызовСервера.ЗаписатьНовоеЗначениеКонстанты("ПерезаписатьНапоминания",истина);
	Закрыть();
	Оповестить("Добавление элемента в раздел",Запись.Элемент)
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если МобильноеПриложениеКлиент Тогда
		Если не ЗначениеЗаполнено(запись.ДатаНапоминания) Тогда
			этаформа.ТекущийЭлемент = элементы.ДатаНапоминания;
			НачатьРедактированиеЭлемента();
		КонецЕсли;
	#КонецЕсли
	Если ЗначениеЗаполнено(запись.Периодичность) Тогда
		Периодическое = истина;
		элементы.ГруппаНапоминания.Видимость = истина;
	КонецЕсли;
	элементы.ЭлементГиперссылка.Заголовок = "Элемент - " + строка(запись.Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПериодическоеПриИзменении(Элемент)
	Если Периодическое Тогда
		элементы.ГруппаНапоминания.Видимость = истина;
		запись.ВремяНапоминания = запись.ДатаНапоминания;
	Иначе
		элементы.ГруппаНапоминания.Видимость = ложь;
		запись.Периодичность = "";
		запись.ВремяНапоминания = дата(1,1,1);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Периодическое Тогда	
		Если запись.Периодичность = "" Тогда
			Отказ = истина;
			сообщить("Необходимо указать периодичность!");
		КонецЕсли;
		Если запись.ДатаПериодическогоНапоминания = дата(1,1,1) Тогда
			запись.ДатаПериодическогоНапоминания = запись.ДатаНапоминания;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЭлементГиперссылкаНажатие(Элемент)
	ОткрытьФорму("Справочник.ЭлементыДоски.ФормаОбъекта",новый Структура("Ключ",запись.Элемент));
КонецПроцедуры


&НаКлиенте
Процедура Отложить(Команда)
	Если Команда.Имя = "ОтложитьНаЧас" Тогда
		запись.ДатаНапоминания = запись.ДатаНапоминания + 3600;	
	ИначеЕсли Команда.Имя = "ОтложитьНаЗавтра" Тогда
		запись.ДатаНапоминания = запись.ДатаНапоминания + 24*3600;
	ИначеЕсли Команда.Имя = "ОтложитьНаПолезавтра" Тогда
		запись.ДатаНапоминания = запись.ДатаНапоминания + 48*3600;
	ИначеЕсли Команда.Имя = "ОтложитьНаНеделю" Тогда
		запись.ДатаНапоминания = запись.ДатаНапоминания + 7*24*3600;
	КонецЕсли;	
КонецПроцедуры

