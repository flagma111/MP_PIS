

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДерево();
КонецПроцедуры

&НаСервере 
Процедура ОбновитьДерево()
	ВерхнийУровень = список.ПолучитьЭлементы();
	ВерхнийУровень.Очистить();
	Сегодня = ВерхнийУровень.Добавить();
	Сегодня.Напоминание = "Сегодня";
	Завтра = ВерхнийУровень.Добавить();
	Завтра.Напоминание = "Завтра";
	Неделя = ВерхнийУровень.Добавить();
	Неделя.Напоминание = "7 дней";
	Далее = ВерхнийУровень.Добавить();
	Далее.Напоминание = "Далее";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегистрСведенийНапоминания.Элемент КАК Элемент,
		|	РегистрСведенийНапоминания.Комментарий КАК Комментарий,
		|	РегистрСведенийНапоминания.ДатаНапоминания КАК ДатаНапоминания,
		|	РегистрСведенийНапоминания.ВремяНапоминания КАК ВремяНапоминания,
		|	РегистрСведенийНапоминания.Периодичность КАК Периодичность,
		|	РегистрСведенийНапоминания.ДатаПериодическогоНапоминания КАК ДатаПериодическогоНапоминания,
		|	ВЫБОР
		|		КОГДА НАЧАЛОПЕРИОДА(РегистрСведенийНапоминания.ДатаНапоминания, ДЕНЬ) <= &ТекущаяДата
		|			ТОГДА 1
		|		КОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(РегистрСведенийНапоминания.ДатаНапоминания, ДЕНЬ, -1), ДЕНЬ) = &ТекущаяДата
		|			ТОГДА 2
		|		КОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(РегистрСведенийНапоминания.ДатаНапоминания, ДЕНЬ, -7), ДЕНЬ) <= &ТекущаяДата
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ КАК Срок
		|ИЗ
		|	РегистрСведений.Напоминания КАК РегистрСведенийНапоминания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаНапоминания";
	
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДата()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Напоминание = строка(Выборка.ДатаНапоминания) + " " + Выборка.Элемент + " " + Выборка.Комментарий; 
		Если Выборка.Срок = 1 Тогда
			Родитель = Сегодня;
		ИначеЕсли Выборка.Срок = 2 Тогда
			Родитель = Завтра;
		ИначеЕсли Выборка.Срок = 3 Тогда
			Родитель = Неделя;
		Иначе
			Родитель = Далее;
		КонецЕсли;
		Нстр = Родитель.ПолучитьЭлементы().Добавить();
		Нстр.Напоминание = Напоминание;
		ЗаполнитьЗначенияСвойств(Нстр,Выборка);
	КонецЦикла;	
КонецПроцедуры//

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДерево();
	Для каждого Строка Из Список.ПолучитьЭлементы() Цикл
		элементы.Список.Развернуть(Строка.получитьИдентификатор(),истина); 
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;	
	СтруктураДляФормированияКлюча = Новый Структура("Ключ", ПолучитьКлюч(ТекущиеДанные.Элемент));
	ОткрытьФорму("РегистрСведений.Напоминания.Форма.ФормаЗаписи", СтруктураДляФормированияКлюча);
КонецПроцедуры
 
&НаСервере 
Функция ПолучитьКлюч(Элемент)
	Возврат РегистрыСведений.Напоминания.СоздатьКлючЗаписи(Новый Структура("Элемент", Элемент));
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Для каждого Строка Из Список.ПолучитьЭлементы() Цикл
		элементы.Список.Развернуть(Строка.получитьИдентификатор(),истина); 
	КонецЦикла;
КонецПроцедуры
